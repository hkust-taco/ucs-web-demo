//  ____              _             _
// |  _ \  _ __  ___ | | _   _   __| |  ___
// | |_) || '__|/ _ \| || | | | / _` | / _ \
// |  __/ | |  |  __/| || |_| || (_| ||  __/
// |_|    |_|   \___||_| \__,_| \__,_| \___|
//
fun (++) concatOp(a, b) = concat(a)(b)
fun (|>) pipe(a, f) = f(a)
fun (!==) notEqual(x, y) = not(x === y)
let anyToString = toString

fun par(s, p) = if p then "(" ++ s ++ ")" else s

type StringOps = {
  length: Int,
  charAt: Int => Str,
  charCodeAt: Int => Int,
  slice: Int => Str
}
declare fun String: nothing
let toStringOps: anything => StringOps = String

type Option[A] = Some[A] | None
class Some[A](value: A)
module None

fun showOption(x) = if x is
  Some(value) then "Some(" ++ toString(value) ++ ")"
  None then "None"
fun mapOption(f, x) = if x is
  Some(value) then Some(f(value))
  None then None
fun (??) getOrElse(x, default) = if x is
  Some(value) then value
  None then default
fun flatten(x) = if x is
  Some(value) then value
  other then other

type List[A] = Cons[A] | Nil
class Cons[A](head: A, tail: List[A])
module Nil
fun (::) cons(head, tail) = Cons(head, tail)
fun reverse(xs) =
  let rec aux(acc, xs) =
    if xs is
      Nil then acc
      Cons(x, xs') then aux(x :: acc, xs')
  aux(Nil, xs)
fun listJoin(sep) =
  let rec aux(acc, xs) =
    if xs is
      Nil then acc
      Cons(x, xs') then aux(acc ++ sep ++ toString(x), xs')
  (xs) =>
    if xs is
      Cons(x, xs') then aux(toString(x), xs')
      Nil then ""
fun showList(xs) = "[" ++ listJoin(", ")(xs) ++ "]"

//  _
// | |     ___ __  __ ___  _ __
// | |    / _ \\ \/ // _ \| '__|
// | |___|  __/ >  <|  __/| |
// |_____|\___|/_/\_\\___||_|
// 

fun isDigit(n) = (48 <= n) && (n <= 57)
fun isBlank(n) = (n === 32) || (n === 9) || (n === 10) || (n === 13)

fun scanInt(text: StringOps, at: Int): Option[[Int, Int]] =
  let rec aux(acc, i: Int): Option[[Int, Int]] = if
    i < 0 then None
    i >= text.length then mapOption(n => [n, i], acc)
    let c = text.charCodeAt(i)
    isDigit(c) then aux(Some((acc ?? 0) * 10 + c - 48), i + 1)
    else mapOption(n => [n, i], acc)
  aux(None, at)

scanInt("a123" |> toStringOps, 0) |> showOption
scanInt("a123" |> toStringOps, 1) |> showOption
scanInt("a123" |> toStringOps, 2) |> showOption
scanInt("a123" |> toStringOps, 3) |> showOption
scanInt("a123" |> toStringOps, 4) |> showOption

fun skipBlank(text: StringOps, at: Int): Int =
  let rec aux(i: Int): Int = if
    i >= text.length then i
    isBlank(text.charCodeAt(i)) then aux(i + 1)
    else i
  aux(at)

skipBlank("abc" |> toStringOps, 0)
skipBlank("abc" |> toStringOps, 1)
skipBlank("abc" |> toStringOps, 2)
skipBlank("  \t\n\r123" |> toStringOps, 0)

class Span(val start: Int, val end: Int)

abstract class Token(val span: Span)
class IntLit(value: Int, span2: Span) extends Token(span2) { // It seems that constructor parameters is not sanitized
  fun toString(): Str = "IntLit(" ++ anyToString(value) ++ ")"
}
class LeftParen(at: Int) extends Token(Span(at, at + 1)) {
  fun toString(): Str = "LeftParen"
}
class RightParen(at: Int) extends Token(Span(at, at + 1)) {
  fun toString(): Str = "RightParen"
}
class BinOp(value: Str, val bp: Int, at: Int) extends Token(Span(at, at + 1)) {
  fun toString(): Str = "BinOp(" ++ value ++ ", " ++ anyToString(bp) ++ ")"
}
class EndOfInput(at: Int) extends Token(Span(at, at)) {
  fun toString(): Str = "EndOfInput"
}
class UnknownInput(rest: Str, at: Int, length: Int) extends Token(Span(at, at + length)) {
  fun toString(): Str = "UnknownInput(" ++ rest ++ ")"
}

fun scanToken(text: StringOps, at: Int): [Token, Int] =
  if
    let at' = skipBlank(text, at)
    at' >= text.length then [EndOfInput(at'), at']
    let head = text.charCodeAt(at')
    head ===
      37 then [BinOp("%", 20, at'), at' + 1]
      40 then [LeftParen(at'), at' + 1]
      41 then [RightParen(at'), at' + 1]
      42 then [BinOp("*", 20, at'), at' + 1]
      43 then [BinOp("+", 10, at'), at' + 1]
      45 then [BinOp("-", 10, at'), at' + 1]
      47 then [BinOp("/", 20, at'), at' + 1]
    (48 <= head) && (head <= 57) and
      scanInt(text, at') is Some([n, at'']) then
        [IntLit(n, Span(at', at'')), at'']
    else [UnknownInput(text.slice(at'), at', text.length - at'), at']

scanToken("bruh" |> toStringOps, 0)
scanToken("1" |> toStringOps, 0)
scanToken("+" |> toStringOps, 0)
scanToken("  42" |> toStringOps, 0)

fun tokenize(str: Str): List[Token] =
  let text = str |> toStringOps
  let rec aux(acc, at) =
    if scanToken(text, at) is [token, at'] and token is
      UnknownInput then (token :: acc) |> reverse
      EndOfInput then acc |> reverse
      else aux(token :: acc, at')
  aux(Nil, 0)

tokenize("0") |> showList
tokenize("1 + 2 * 3") |> showList
tokenize("bruh") |> showList

//  ____                _
// |  _ \ __ _ _ __ ___(_)_ __   __ _
// | |_) / _` | '__/ __| | '_ \ / _` |
// |  __/ (_| | |  \__ \ | | | | (_| |
// |_|   \__,_|_|  |___/_|_| |_|\__, |
//                              |___/

type Expr = IntLit | Binary
class Binary(op: BinOp, left: Expr, right: Expr)

fun precedence(t: Expr): Int = if t is
  IntLit then 30
  Binary(op, _, _) then op.bp

fun showExpr(t: Expr): Str = if t is
  IntLit(n) then anyToString(n)
  Binary(BinOp(op, bp), left, right) then
    let lbp = precedence of left
    let rbp = precedence of right
    par(showExpr(left), lbp < bp) ++ " " ++ op ++ " " ++ par(showExpr(right), rbp < bp)

let s = Span(0, 0)
IntLit(42, s) |> showExpr
let t1 = Binary(BinOp("+", 10, 0), IntLit(1, s), IntLit(2, s))
t1 |> showExpr
let t2 = Binary(BinOp("*", 20, 0), t1, IntLit(3, s))
t2 |> showExpr
let t3 = Binary(BinOp("*", 20, 0), t2, IntLit(4, s))
t3 |> showExpr

type ParseResult[A] = Some[A] | Failed
class Failed(message: Str)
fun showParseResult(r: ParseResult['A]) = if r is
  Some(value) then "Some(" ++ toString(value) ++ ")"
  Failed(message) then "Failed(" ++ message ++ ")"
fun (?>) mapParseResult(x, f) = if x is
  Some(value) then Some(f(value))
  failed then failed

fun showParsedExpr(r: ParseResult[Expr]) = if r is
  Some(value) then "Some(" ++ showExpr(value) ++ ")"
  Failed(message) then "Failed(" ++ message ++ ")"

fun lastPosition(t: Expr): Int = if t is
  IntLit(_, span) then span.end
  Binary(_, _, right) then lastPosition of right

fun parseAtom(ts: List[Token]): ParseResult[[Expr, List[Token]]] =
  if ts is
    Cons(IntLit(n, span), ts') then Some([IntLit(n, span), ts'])
    Cons(LeftParen, ts') and parseExpr(0, ts') is
      Some([body, Cons(RightParen, ts'')]) then Some([body, ts''])
      Some([body, _]) then Failed("Expected a right parenthesis at " ++ toString(lastPosition of body))
      failed then failed
    Cons(token, _) then Failed("Unexpected token " ++ toString(token) ++ " at " ++ toString(token.span.start))
    Nil then Failed("Unexpected end of input")

fun parseExpr(bp: Int, ts: List[Token]): ParseResult[[Expr, List[Token]]] =
  if parseAtom(ts) is
    Some([leftmost, ts']) then
      let rec aux(left, ts) = if ts is
        Cons(BinOp(op, bp', opAt), ts') and bp < bp' and
          parseExpr(bp', ts') is
            Some([right, ts'']) then
              aux(Binary(BinOp(op, bp', opAt), left, right), ts'')
            failed then failed
        else Some([left, ts])
      aux(leftmost, ts')
    failed then failed

fun parse(source: Str): ParseResult[Expr] =
  if parseExpr(0, tokenize(source)) is
    Some([expr, Nil]) then Some(expr)
    Some([expr, rest]) then Failed("Unexpected token: " ++ showList(rest) ++ " at " ++ toString(lastPosition of expr))
    failed then failed

parse("1 + 2 * 3") |> showParsedExpr
parse("(1 + 2) * 3") |> showParsedExpr
parse("2 * (1 + 3 + 5 + 7 + 9 + 11) - 2 - 4 - 6") |> showParsedExpr
parse("2 * (1 + 3) * (5 + 7) * (9 - 11)") |> showParsedExpr
parse("(((((((((42)))))))))") |> showParsedExpr

parse("1 + ") |> showParsedExpr
parse("1 bruh") |> showParsedExpr
parse("1 * (2 + 3") |> showParsedExpr
parse("1 - bruh") |> showParsedExpr

//  _____            _             _   _
// | ____|_   ____ _| |_   _  __ _| |_(_) ___  _ __
// |  _| \ \ / / _` | | | | |/ _` | __| |/ _ \| '_ \
// | |___ \ V / (_| | | |_| | (_| | |_| | (_) | | | |
// |_____| \_/ \__,_|_|\__,_|\__,_|\__|_|\___/|_| |_|
//

fun evaluate(t: Expr): Option[Int] =
  if t is
    IntLit(n) then Some(n)
    Binary(BinOp(op, _, _), left, right) and
      evaluate(left) is Some(leftResult) and
        evaluate(right) is Some(rightResult) and op ===
          "+" then Some(leftResult + rightResult)
          "-" then Some(leftResult - rightResult)
          "*" then Some(leftResult * rightResult)
          // "/" then Some(leftResult / rightResult)
          "%" then Some(leftResult % rightResult)
    else None

fun showEvaluation(source: Str): Str =
  if parse(source) is
    Some(expr) and evaluate(expr) is
      Some(result) then toString(result)
      None then "Evaluation failed"
    Failed(message) then "Parsing failed: " ++ message

showEvaluation of "1 + 2 * 3"
showEvaluation of "(((((42)))))"
showEvaluation of "1 * (3 + 4) - 5"
showEvaluation of "1 + "
showEvaluation of "1 bruh"
